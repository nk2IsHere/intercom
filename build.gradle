import java.text.SimpleDateFormat

plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.72'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.jetbrains.kotlin.plugin.noarg' version '1.3.72'
}

repositories {
    mavenCentral()
}

def gitTag = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

subprojects { subproject ->
    subproject.apply plugin: 'org.springframework.boot'
    subproject.apply plugin: 'io.spring.dependency-management'
    subproject.apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    subproject.apply plugin: 'org.jetbrains.kotlin.jvm'
    subproject.apply plugin: 'org.jetbrains.kotlin.plugin.noarg'

    group = "eu.nk2"
    version = gitTag()
    java.sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.nk2.eu/artifactory/gradle-release' }
    }

    compileJava.inputs.files(processResources)

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
            languageVersion = "1.3"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    jar {
        manifest {
            attributes(
                'Project-Name': subproject,
                'Version': gitTag(),
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            )
        }
    }

    bootJar {
        launchScript()
        manifest {
            attributes(
                'Project-Name': subproject,
                'Version': gitTag(),
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            )
        }
    }
}
